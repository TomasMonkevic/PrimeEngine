R"( $version 330 core/$

$vertex

layout (location = 0) in vec3 position;
layout (location = 1) in vec4 color;
layout (location = 2) in vec3 normal;

uniform mat4 pr_matrix;
uniform mat4 view_matrix = mat4(1.0f);
uniform mat4 model_matrix = mat4(1.0f);

out data
{
	vec4 color;
	vec3 normal;
	vec3 position;
} dataOut;

void main()
{
	gl_Position = pr_matrix * view_matrix * model_matrix * vec4(position, 1.0f);
	dataOut.color = color;
	dataOut.normal = mat3(transpose(inverse(model_matrix))) * normal;
	dataOut.position = vec3(model_matrix * vec4(position, 1.0f)).xyz;
}/$

$fragment

struct Light
{
	vec3 position;
	vec4 color;
	float intensity;
};

in data
{
	vec4 color;
	vec3 normal;
	vec3 position;
} dataIn;

layout (location = 0) out vec4 color;

uniform Light light;
uniform vec3 viewPosition; //TODO change to view direction

void main()
{
	//Ambient light
	vec4 ambient = (light.intensity * mix(light.color,vec4(1.0f), 0.9f));
	ambient.w = 1.0f;

	//Diffuse light
	vec3 norm = normalize(dataIn.normal);
	vec3 lightDir = normalize(light.position - dataIn.position);
	vec4 diffuse = max(dot(norm, lightDir), 0.0f) * mix(light.color,vec4(1.0f), 0.9f);
	diffuse.w = 1.0f;

	//Specular light
	vec3 viewDir = normalize(viewPosition - dataIn.position);
	vec3 lightReflection = normalize(reflect(-lightDir, norm));
	float spec = pow(max(dot(lightReflection, viewDir), 0.0f), 64);
	vec4 specular = spec * 0.2 * mix(light.color,vec4(1.0f), 0.5f);
	specular.w = 1.0f;

	color = (ambient + diffuse + specular) * dataIn.color;
	//color = vec4(abs(dataIn.normal.x), abs(dataIn.normal.y), abs(dataIn.normal.z), 1.0f);
}
/$ )"